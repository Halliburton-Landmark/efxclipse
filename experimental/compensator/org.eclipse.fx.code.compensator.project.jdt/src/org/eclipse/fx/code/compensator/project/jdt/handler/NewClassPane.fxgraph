package org.eclipse.fx.code.compensator.project.jdt.handler

import org.eclipse.fx.ui.panes.GridLayoutPane
import javafx.scene.control.Label
import javafx.scene.control.TextField
import org.eclipse.fx.ui.panes.GridData
import javafx.scene.control.Button
import javafx.scene.control.CheckBox
import javafx.scene.control.RadioButton
import javafx.scene.control.ToggleGroup
import org.eclipse.fx.code.compensator.project.jdt.handler.NewClassPaneController

component NewClassPane controlledby NewClassPaneController {
	define ToggleGroup id visibilityMods
	GridLayoutPane {
		numColumns : 3,
		children : [
			Label {
				text : "Package"
			},
			TextField id packageField {
				static constraint : GridData {
					grabExcessHorizontalSpace : true,
					horizontalAlignment : "FILL"
				}
			},
			Button {
				text : "Browse ..."
			},
			// ----------
			Label {
				text : "Name"
			},
			TextField id className {
				static constraint : GridData {
					grabExcessHorizontalSpace : true,
					horizontalAlignment : "FILL"
				}
			},
			Label,
			// ----------
			Label {
				text : "Modifier"
			},
			GridLayoutPane {
				numColumns : 3,
				children : [
					RadioButton id publicModifier {
						text : "public",
						selected : true,
						toggleGroup : idref visibilityMods
					},
					RadioButton id packageModifier {
						text : "package",
						toggleGroup : idref visibilityMods
					},
					RadioButton id protectedModifier {
						text : "protected",
						toggleGroup : idref visibilityMods
					}
				]
			},
			Label,
			// ----------
			Label,
			GridLayoutPane {
				numColumns : 4,
				children : [
					CheckBox id abstractModifier {
						text : "abstract"
					},
					CheckBox id finalModifier {
						text : "final"
					}
				]
			},
			Label
		]
	}
}
